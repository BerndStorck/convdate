#! /bin/sed -f
#
# convdemo, 1.0.2
#
# Demonstrates the manipulations of the sed script convdate 2.1.0, which
# converts a German date in an English format, f. e. "20.05.2018" to
# 2018/05/20.
#
# Bernd Storck, 2018-05-25, 2020-10-08
# Contact: https://www.facebook.com/BStLinux/

p

s/ //g  					# Delete spaces.
T tabs
s/.*/& Leerzeichen entfernt,/

: tabs
s/[\t]//g  					# Delete tabs.
T names
s/.*/& Tabulatoren entfernt,/

# Replaces month names by numbers independent of the fact, if the first letter is in upper or lower case:
:names
s/[Jj]anuar/01\./
s/[Ff]ebruar/02\./
s/[Mm]ärz/03\./
s/[Mm]aerz/03\./
s/[Aa]pril/04\./
s/[Mm]ai/05\./
s/[Jj]uni/06\./
s/[Jj]uli/07\./
s/[Aa]ugust/08\./
s/[Ss]eptember/09\./
s/[Oo]ktober/10\./
s/[Nn]ovember/11\./
s/[Dd]ezember/12\./
T abbrev
s/.*/& Namen ersetzt,/

# Same as before, but for the common German month abbreviations:
:abbrev
s/[Jj]an\./01\./
s/[Ff]eb\./02\./
s/[Aa]pr\./04\./
s/[Jj]un\./06\./
s/[Jj]ul\./07\./
s/[Aa]ug\./08\./
s/[Ss]ept\./09\./
s/[Oo]kt\./10\./
s/[Nn]ov\./11\./
s/[Dd]ez\./12\./
T onedigit
s/.*/& Abkürzung ersetzt,/

:onedigit
/^[[:digit:]]\./s/\([[:digit:]]\.\)/0\1/	# Inserts 0 before day number, if it's a single digit.
s/\.\([[:digit:]]\.\)/\.0\1/			# Adds 0 before month number, if it's a single digit.
T invert
s/.*/& Einzelziffern ergänzt,/

:invert
s:\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\):\3/\2/\1:	# Inverts number sequence, "1.2.3" becomes "3/2/1".
T end
s/.*/& umgekehrt/

: end
a\
